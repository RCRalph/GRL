<lower_letter> ::= "a" | "b" | "c" | ... | "z"
<upper_letter> ::= "A" | "B" | "C" | ... | "Z"
<digit> ::= "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"
<underscore> ::= "_"
<dot> ::= "."
<minus> ::= "-"
<identifier_character> ::= <lower_letter> | <underscore>
<identifier_character_or_digit> ::= <identifier_character> | <digit>
<line_separator> ::= "\n" | ";"

<symbol> ::= <lower_letter> | <upper_letter> | <digit> | <underscore> | <dot>
<text> ::= "" | <text> <symbol>
<string> ::= '"' <text> '"'
           | <string> "+" <string>
           | <string> "+" <number>
           | <string> "+" <bool_expr>
           | <number> "+" <string>
           | <bool_expr> "+" <string>
           | "(" <string> ")"
           | "(" "STR" <string> ")"
           | "(" "STR" <number> ")"
           | "(" "STR" <bool_expr> ")"
           | "(" "STR" <identifier> ")"

<identifier> ::= <identifier_character> | <identifier> <identifier_character_or_digit>
<bool_literal> ::= "TRUE" | "FALSE"

<digit_sequence> ::= <digit> | <digit_sequence> <digit>

<number> ::= <digit_sequence>
           | <digit_sequence> <dot> <digit_sequence>
           | <minus> <digit_sequence>
           | "NODE COUNT" <identifier>
           | "EDGE COUNT" <identifier>
           | "GET WEIGHT OF EDGE" <edge> <identifier>
           | "DISTANCE BETWEEN" <edge> <identifier>
           | "LENGTH" <single_iterator>
           | "LENGTH" <double_iterator>
           | "LENGTH" <triple_iterator>
           | ( "NUM" <identifier> )
           | <number> "+" <number>
           | <number> "-" <number>
           | <number> "*" <number>
           | <number> "/" <number>
           | <number> "**" <number>
           | "(" <number> ")"
           | "(" "NUM" <string> ")"
           | "(" "NUM" <number> ")"
           | "(" "NUM" <bool_expr> ")"
           | "(" "NUM" <identifier> ")"

<comparator> ::= "==" | "!=" | "<" | "<=" | ">" | ">="

<bool_expr> ::= <bool_literal>
              | <comparison>
              | "HAS NODE" <node> <identifier>
              | "HAS EDGE" <edge> <identifier>
              | "IS" <graph_type> <identifier>
              | <string> <comparator> <string>
              | <string> <comparator> <number>
              | <string> <comparator> <bool_expr>
              | <number> <comparator> <string>
              | <number> <comparator> <number>
              | <number> <comparator> <bool_expr>
              | <bool_expr> <comparator> <string>
              | <bool_expr> <comparator> <number>
              | <bool_expr> <comparator> <bool_expr>
              | <identifier> <comparator> <identifier>
              | "NOT" <bool_expr>
              | <bool_expr> "AND" <bool_expr>
              | <bool_expr> "OR" <bool_expr>
              | <bool_expr> "XOR" <bool_expr>
              | <bool_expr> "=>" <bool_expr>
              | "(" <bool_expr> ")"
              | "(" "BOOL" <string> ")"
              | "(" "BOOL" <number> ")"
              | "(" "BOOL" <bool_expr> ")"
              | "(" "BOOL" <identifier> ")"

<node> ::= <string> | <identifier>
<edge> ::= <node> <node>

<graph_type> ::= "GRAPH" | "DIGRAPH"
<entity> ::= <graph_type> | "NODE" <node> | "EDGE" <edge>

<add_operation> ::= "ADD" <entity> <identifier>
<rm_operation> ::= "RM" <entity> <identifier>
<set_operation> ::= "SET WEIGHT OF EDGE" <edge> <number> <identifier>

<single_iterator> ::= "NODES" <identifier>
                    | "TOPOLOGICAL SORT" <identifier>
                    | "SHORTEST PATH" <edge> <identifier>
                    | "NEIGHBORS" <node> <identifier>

<double_iterator> ::= "EDGES" <identifier>
                    | "DISTANCE FROM" <node> <identifier>
                    | "DFS" <node> <identifier>
                    | "BFS" <node> <identifier>

<triple_iterator> ::= "DISTANCE MATRIX" <identifier>

<if_statement> ::= "IF" <bool_expr> {" <program> "}" { <elseif_statement> } [ <else_statement> ]
<elseif_statement> ::= "ELSEIF" <bool_expr> "{" <program> "}"
<else_statement> ::= "ELSE" "{" <program> "}"

<for_loop> ::= "FOR" <identifier> "OF" <single_iterator> "{" <program> "}"
             | "FOR" <identifier> "," <identifier> "OF" <double_iterator> "{" <program> "}"
             | "FOR" <identifier> "," <identifier> "," <identifier> "OF" <triple_iterator> "{" <program> "}"

<print> ::= "PRINT" <bool_expr>
          | "PRINT" <number>
          | "PRINT" <string>
          | "PRINT" <identifier>

<draw> ::= "DRAW" <identifier>
<export> ::= "EXPORT" <identifier> <string>
<import> ::= "IMPORT" <identifier> <string>
<run> ::= "RUN" <string>
<exit> ::= "EXIT"

<set> ::= "SET" <identifier> <string>
        | "SET" <identifier> <number>
        | "SET" <identifier> <bool_expr>
        | "SET" <identifier> <identifier>

<statement> ::= <if_statement>
              | <for_loop>
              | <add_operation>
              | <rm_operation>
              | <set_operation>
              | <print>
              | <draw>
              | <export>
              | <import>
              | <run>
              | <exit>
              | <set>
              | ""

<statement_sequence> ::= <statement> { <line_separator> <statement> }
<program> ::= <statement_sequence>
